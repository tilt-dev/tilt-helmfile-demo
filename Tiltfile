# -*- mode: Python -*-

# Helper function to read K8s config YAML from helmfile.
#
# Currently `helmfile template` has a bug where it prints messages to stdout instead of stderr
# https://github.com/roboll/helmfile/issues/551
# So we use the workaround suggested there.
def helmfile(file):
  return local("helmfile -f %s template | grep -v -e '^Decrypting .*' | grep -v -e '^Fetching .*' | grep -v 'as it is not a table.$'" % file)

def mutate_yaml(x, f):
  if type(x) == 'string':
    objects = read_yaml_stream(x)
  elif type(x) == 'blob':
    objects = decode_yaml_stream(x)
  else:
    fail('only takes string or blob, got: %s' % type(x))

  return encode_yaml_stream([f(o) for o in objects])

def yaml_with_namespace(x, ns):
  """
  Takes K8s yaml, sets its namespace to `ns`, and returns it as a blob.

  This modifies the yaml in two ways:
  1. Sets .metadata.namespace to `ns`
  2. Sets ..template.metadata.namespace to `ns`
     This ensures the namespace in, e.g., Deployment Pod Template Specs is
     set, but might have false positives if you have a CRD with some other
     element named 'template'.

  Args:
    x: K8s yaml. Either a filename (string) or the yaml itself (Blob)
    ns: The namespace to set the K8s objects to.

  Returns:
    Blob containing the K8s object as yaml, with namespaces set to `ns`.
  """
  return mutate_yaml(x, lambda o: set_k8s_yaml_namespace(o, ns))
  # return 6

def set_k8s_yaml_namespace(o, ns):
  o['metadata']['namespace'] = ns
  _set_template_namespace(o, ns)
  return o

def _set_template_namespace(o, ns):
  if type(o) == 'dict':
    for k, v in o.items():
      if k == 'template':
        v['metadata']['namespace'] = ns
      if type(v) == 'dict' or type(v) == 'list':
        _set_template_namespace(v, ns)
  elif type(o) == 'list':
    for v in o:
      _set_template_namespace(v, ns)

# Tell Tilt to apply to k8s config generated by helmfile.
k8s_yaml(yaml_with_namespace(helmfile("k8s/staging/helmfile.yaml"), 'kube-public'))
